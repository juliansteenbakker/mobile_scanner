group 'dev.steenbakker.mobile_scanner'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.22"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'dev.steenbakker.mobile_scanner'
    }

    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
        consumerProguardFiles 'proguard-rules.pro'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }
}

dependencies {
    def useUnbundled = project.findProperty('dev.steenbakker.mobile_scanner.useUnbundled') ?: false
    if (useUnbundled.toBoolean()) {
        // Dynamically downloaded model via Google Play Services
        implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.0'
    } else {
        // Bundled model in app
        implementation 'com.google.mlkit:barcode-scanning:17.2.0'
    }

    implementation 'androidx.camera:camera-lifecycle:1.3.2'
    implementation 'androidx.camera:camera-camera2:1.3.2'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.mockito:mockito-core:5.11.0'    
}
